// Branwen's ToB script. Read this after SoA script, O#BranS.baf

// Branwen talks immediately when summoned.
IF
Global("O#BranSummoned","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet([PC])
Wait(1)
END

// Starting ToB Talks
IF 
InParty(Myself)
Global("O#BranShutUp","GLOBAL",0)
Global("O#BranTalkToB","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("O#BranTimerToB","GLOBAL",1800)
SetGlobal("O#BranTalkToB","GLOBAL",1)
END

// Just like with SoA, we have a big block for Branwen's talks: when the timer expires, variable sets to 2,4,6... and Branwen talks.
IF
InParty(Myself)
RealGlobalTimerExpired("O#BranTimerToB","GLOBAL")
Global("O#BranShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(5)
Global("O#BranTalkToB","GLOBAL",1)
Global("O#BranTalkToB","GLOBAL",3)
Global("O#BranTalkToB","GLOBAL",5)
Global("O#BranTalkToB","GLOBAL",7)
Global("O#BranTalkToB","GLOBAL",9)
THEN
RESPONSE #100
IncrementGlobal("O#BranTalkToB","GLOBAL",1)
END

// No music for non-romanced Branwen, music for the romanced one
IF
InParty(Myself)
Global("O#BranShutUp","GLOBAL",0)
Global("O#BranRomanceActive","GLOBAL",2)
!AreaType(DUNGEON)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(5)
Global("O#BranTalkToB","GLOBAL",2)
Global("O#BranTalkToB","GLOBAL",4)
Global("O#BranTalkToB","GLOBAL",6)
Global("O#BranTalkToB","GLOBAL",8)
Global("O#BranTalkToB","GLOBAL",10)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("O#BRAN")
StartDialogueNoSet(Player1)
END

IF
InParty(Myself)
Global("O#BranShutUp","GLOBAL",0)
!Global("O#BranRomanceActive","GLOBAL",2)
!AreaType(DUNGEON)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(5)
Global("O#BranTalkToB","GLOBAL",2)
Global("O#BranTalkToB","GLOBAL",4)
Global("O#BranTalkToB","GLOBAL",6)
Global("O#BranTalkToB","GLOBAL",8)
Global("O#BranTalkToB","GLOBAL",10)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Gorion's Wraith, romance only
IF
InParty(Myself)
Global("O#BranShutUp","GLOBAL",0)
Global("O#BranWraithSpirit1","GLOBAL",1)
Global("O#BranRomanceActive","GLOBAL",2)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
See(Player1)
THEN
RESPONSE #100
SetGlobal("O#BranWraithSpirit1","GLOBAL",2)
END

IF
InParty(Myself)
Global("O#BranWraithSpirit1","GLOBAL",2)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("O#BRAN")
StartDialogueNoSet(Player1)
END

// Saradush sacked, romance only, at waking up
IF
InParty(Myself)
Global("O#BranShutUp","GLOBAL",0)
Global("O#BranSaradush","GLOBAL",0)
Global("O#BranRomanceActive","GLOBAL",2)
Dead("yaga01")
PartyRested()
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
THEN
RESPONSE #100
SetGlobal("O#BranSaradush","GLOBAL",1)
END

IF
InParty(Myself)
Global("O#BranSaradush","GLOBAL",1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("O#BRAN")
StartDialogueNoSet(Player1)
END

// Third challenge, Innocence or Doubt
IF
InParty(Myself)
Global("O#BranShutUp","GLOBAL",0)
Global("O#BranChallenge3","GLOBAL",0)
GlobalGT("BeginChallenge3","GLOBAL",2) // Third challenge happened
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
IncrementGlobal("O#BranChallenge3","GLOBAL",1)
END

IF
InParty(Myself)
Global("O#BranRomanceActive","GLOBAL",2)
Global("O#BranChallenge3","GLOBAL",1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("O#BRAN")
StartDialogueNoSet(Player1)
END

IF
InParty(Myself)
!Global("O#BranRomanceActive","GLOBAL",2)
Global("O#BranChallenge3","GLOBAL",1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Fifth challenge, Ravager's dead, final kiss, romance only
IF
InParty(Myself)
Global("O#BranShutUp","GLOBAL",0)
Global("O#BranChallenge5","GLOBAL",0)
Global("O#BranRomanceActive","GLOBAL",2)
Dead("CHALRV01")    // Ravager's dead
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
IncrementGlobal("O#BranChallenge5","GLOBAL",1)
END

IF
InParty(Myself)
Global("O#BranChallenge5","GLOBAL",1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("O#BRAN")
StartDialogueNoSet(Player1)
END

// Flirts for Branwen: Branwen-initiated flirts

// Flirts initiate...
IF
InParty(Myself)
Global("O#BranShutUp","GLOBAL",0)
Global("O#BranRomanceActive","GLOBAL",2)
Global("O#BranFlirtsStartToB","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("O#BranFlirtsStartToB","GLOBAL",1)      
RealSetGlobalTimer("O#BranFlirtTimerToB","GLOBAL",300)
END

// Ready for flirting!
IF
InParty(Myself)
RealGlobalTimerExpired("O#BranFlirtTimerToB","GLOBAL")
Global("O#BranShutUp","GLOBAL",0)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("O#BranRomanceActive","GLOBAL",2)
See(Player1)
CombatCounter(0)
!See([ENEMY])
!AreaCheck("AR6200")                  // No flirting during the final battle
Global("O#BranFlirtToB","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("O#BranFlirtToB","GLOBAL",1)             
END

// Flirting begins!
IF
InParty(Myself)
Global("O#BranFlirtToB","GLOBAL",1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// If anything's wrong with Branwen's soundset, select her and press K
IF
HotKey(K)
THEN
RESPONSE #100
StartCutSceneMode()
StartCutScene("O#BranF")
END